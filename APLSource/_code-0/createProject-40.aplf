 createProject←{
     dat.loadtime←DT.ts2dt ⎕TS⊣dat←⎕NS''
     ∨/nil⊢folder nspath←2↑⊂if≡⍵:{0}logMsg MS.DY
     dat.folder←fPath folder,'/'
     _NEXISTS dat.folder:{0}logMsg MS.DF
     '⎕'∊dat.nspath←⊃prefSpace⊂nspath:{0}logMsg MS.DC
     ~((1=≢)∨0∘ssp∨0=⎕NC)dat.nspath:{0}logMsg MS.DC ⍝ 1=≢ => ,'#'

     dat.(hist case vars)←On¨OPT.(keephistory casecode variables)
     dat.(config source code changes latest)←{
         ⍵}dat.folder∘,¨MS.HO MS.GY,MS.NB∘,¨''MS.EU MS.HE
     z←dat.nspath∘{dataSpaces ⍵/⍨⍵.nspath∊⊂⍺}if(×∘≢)openProjects''
     z←fixQV⊢space←⍎dat.nspath ⎕NS''
     z←3 _MKDIR¨dat.(source changes)/⍨1,dat.hist
     (cfg←configFields'').ProjectSpace←dat.nspath
     cfg.(KeepHistory CaseCode Variables)←dat.(⊂hist case vars)⌷MS.GB MS.GA
     z←(_JSON cfg)_NPUT dat.config⊣cfg.⎕EX'Open' ⍝ deprecated
     z←'dat'⎕NS lcNs cfg ⍝ ← into dat ↑ write
     z←(MS.NB MS.FX)_NPUT dat.folder,MS.FM ⍝ git specific -
     fqn←validItems⊢nms←(⊢(/⍨)0<⎕NC)space UT.li⍳11
     fqn/⍨←dat.vars∨2≠⌊⎕NC fqn
     fqn(dat caseOK⍨)←fqn⊣notStored{⍵/⍨0=0 ssp ⍵}nms~fqn
     dat.cfg←⎕NS cfg⊣space MS.FV _is_ cfg⊣cfg.ProjectFolder←dat.folder
     fqn←⊃dat putSource getValue fqn
     dat.aftersave←SV.aftersave
     dat⊣dat.loadtime,←DT.ts2dt ⎕TS⊣dat.changed←''
⍝ ⍵ path [space]
⍝ ← dat or 0
⍝ see openProject CreateProject
⍝ Phil 2019-08-22 06:38 5.2.0+244
 }
