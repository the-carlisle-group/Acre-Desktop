 getSource←{dat←⍺ ⋄ ref←⍵≡MS.BG ⋄ prev←dat getTime''
     src tgt←dat.(source nspath),¨'' '.'           ⍝ ../APLSource/
     read←{⊃MS.HI ⎕NGET ⍵ 1}
     (path date)←{⍵.(Path Date)}fileTree⊢src       ⍝ all files
     path date/⍨←⊂~∨/'/.'⍷(≢src)↓⍤1↑path           ⍝ ignore all /.*/
     file type←↓⍉↑(UT.px(≢src)↓⊢)¨path             ⍝ 'ns/path/file' '.type'
     path date file type/⍨←∊/Cc type(MS.AZ,MS.AY)  ⍝ only valid types
     name←'/.'decodeCaps file
     path date file name type/⍨←⊂ref≤new←date>prev ⍝ new or not refresh
⍝     z←logMsg MS.DD showList,/(tgt∘,¨dn/name),'=',⍪type/⍨dn←multi name
     z←MS.DD logMsg,/(tgt∘,¨dn/name),'=',⍪type/⍨dn←multi name
     z←logMsg MS.BC,MS.EO,((≢path)UT.pl MS.FL),MS.EY,¯4 tPath src
     val←3 read ticks path ⋄ fqn←tgt∘,¨name
     fqn type val msk←1 fixValue fqn type val      ⍝ name/type-change?
     z←logMsg MS.FW∘,¨(¯4 tPath¨path/⍨~msk),¨⊂MS.BP ⍝ file needs att'n
     fqn type val path date/⍨←⊂msk                 ⍝ remove unfixed
     z←dat renameSource path fqn                   ⍝ if different
     z←dat putTime''
     ~dat.hist:fqn if ref⊢⍵
     chgs←⊃(⊣(/⍨)prev<⊢)/dat changes''             ⍝ new in changes/
     diff←(fqn∊chgs)<prev<date                     ⍝ new in APLSource/
     msg←MS.EN,((+/diff)UT.pl MS.FN),MS.EZ,¯4 tPath dat.changes
⍝     z←logMsg msg showList diff/fqn
     z←msg logMsg diff/fqn
     fqn←dat putChange diff∘/¨fqn val type
     fqn if ref⊢⍵
⍝ ⍺ dat
⍝ ⍵ ? or MS.BG ('Refresh')
⍝ ← ⍵ or fqns of refreshed items
⍝ get source and fix; if refresh fetch only changes
⍝ Phil 2018-07-18 19.47
 }
