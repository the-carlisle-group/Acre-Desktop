 itemMethod←{
     0∊⍴ids←openProjects'':''⊣logMsg MS.GX
     null⊢items←prefSpace ⍵:''⊣logMsg MS.DH
     ok←0≤⎕NC items
     ok∧←ok\0≠dat←whichProject ok/items
     z←MS.DI logMsg items/⍨~ok
     items dat/⍨←⊂ok
     0∊⍴items:''⊣logMsg MS.DH
⍝       ↓↓↓↓
     ⊃if≢⍺⍺/dat{⍺ ⍵}⌸items

⍝ ⍺⍺   method to run - takes instance data space left arg
⍝ ⍵    args
⍝ args random list of items of possiby disparate projects
⍝ Run instance method ⍺⍺ for each subgroup of ⍵
⍝   corresponding to a tracked project
⍝   Methods expected to take 1 or more names and return a subset
⍝ ← results per project, '' if none
⍝ Phil 2019-08-10 16.09 5.2.0+227
 }
