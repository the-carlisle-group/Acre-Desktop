 openLoad←{dat←⍺ ⋄ trk dep←⍵             ⍝ as of caller
     ~null MS.DE logMsg dat∘openConflict⍣trk⊢'':0 ⍝ can't open this
     ~dep:1                              ⍝ ok but no deps
     pref←{
         p←('/',⍺)[('\',⍺)⍳⍺]
         (':'∘∊∨'/'=⊃)p:p ⍵              ⍝ absolute path
         '.'=⊃p:(dat.folder,'../',p)⍵    ⍝ ../uncle/cousin/nephew
         (dat.folder,(⊃,/(1++/p∊'/\')/,/'../'),p)⍵ ⍝ uncle/cousin
     }
     0∊⍴these←nout⊂if≡dat.open:1        ⍝ ex config
     nss←(⎕NEW ⎕SE.Parser(optionFields'dep track')).Parse¨these
     args←nss.Arguments
     args nss/⍨←⊂~args∊SV.arguments      ⍝ this time
     0∊⍴SV.arguments,←args:1
     track←trk∧nOff¨nss.track
     depend←dep∧nOff¨nss.dependencies
     dts←(⊂trk dep)openProject¨↓track,depend,⍪pref/↑2↑¨args
     dts track depend/⍨←⊂dts≠0           ⍝ only opened
     z←dataSpaces track/dts              ⍝ keep only tracked dependencies
     ~null MS.DE logMsg dat∘openConflict⍣trk⊢'':{0}logMsg dat.config MS.CB
     1:1
⍝ dyadic calls to openProject tell it NOT to run Startup
⍝ Open dependent projects
⍝ this is potentially endlessly recursive
⍝ Phil 2018-06-06 21.11
 }
