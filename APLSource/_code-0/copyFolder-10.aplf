 copyFolder←{⍺←⊢ ⋄ era←1=⍺⊣0
     source target←'/'UT.nn¨src tgt←¯1↓¨fPath¨⍵,¨'/'
     n←≢/Cc source target
     tgt,←n/'/',source
     z←logMsg MS.EI
     sp tp←{
         s←fileTree src
         z←3 ⎕MKDIR tgt
         t←fileTree tgt
         (s t).Path
     }UT.tk 0
     z←logMsg MS.EF,(≢sp)UT.pl MS.FL
     del copy←{
         sp←(⍴src)↓¨sp
         tp←(⍴tgt)↓¨tp
         s t←Cc sp tp
         del←tgt∘,¨tp/⍨~t∊s
         miss←↓⍉src tgt∘.,sp/⍨~mem←s∊t
         both←src tgt∘.,sp/⍨mem
         hash←{2 ⎕NQ'.'MS.HJ ⍵}
         diff←↓⍉both/⍨~≡⌿hash¨both
         del(miss,diff)
     }UT.tk 0
     del/⍨←era
     z←logMsg era/MS.ET,(≢del)UT.pl MS.FL
     z←⎕NDELETE⍨/UT.tk del,1
     z←logMsg MS.ES,(≢copy)UT.pl MS.FL
     z←(copyFile⊣)/UT.tk copy,0
     ⊢/↑copy
⍝ copy only missing and different.
⍝ [⍺] 1: delete from target if not in source
⍝ ⍵ source and target paths.
⍝   if last names differ then folder with
⍝   name of source is created inside target.
⍝ e.g.  copyFolder 'C:/alpha/bravo' 'D:/charlie/delta'
⍝       creates 'D:/charlie/delta/bravo'
⍝ while copyFolder 'C:/echo/foxtrot' 'D:/golf/foxtrot'
⍝       uses 'D:/golf/foxtrot'
⍝ ← copied paths
 }
