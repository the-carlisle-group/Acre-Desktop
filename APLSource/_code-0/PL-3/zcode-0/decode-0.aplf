 decode←{
     mon←{0}arg←1/⍵
     ⍺←{dix 0}arg←flatten arg⊣mon←1          ⍝ once
     arg≢r←⍺ pull arg:r                     ⍝ done?
     put←⍺ arg push⊢                        ⍝ fn to store val
     next←⍺ ∇⊢
     d←(arg='⋄')>q←quoted arg               ⍝ diamonds / quotes
     ∧/q∨arg='''':put⍎arg
     z b x bp←↓-⍀+\q<⍤1⊢'[]()'∘.=arg
     p s←(bp-b)(+\2≠/0,bp)
     z←errors⍣mon⊢arg b bp d p q s          ⍝ check syntax once
     x b p bp s d q arg/⍨←⊂1+x←q<>⌿0 1⌽'[⋄'∘.=arg
     (x/arg)←'[⋄'⍴⍨+/x                      ⍝ '⋄' after '['
     (x/d)←0 1⍴⍨+/x
     ~∨/d:put⍎arg
     pt←((2</⊢)∨¯1⌽2>/⊢)¯1,1⌊bp
     exec←arg⊣arg d bp q⊂⍨←⊂pt
     (x/exec)←{'(',⍵,'⊃arg)'}∘⍕¨x/⍳⍴x←1∊¨d
     (x/arg)←next part¨x/arg
     put⍎⊃,/exec

⍝ ⍵ ⋄-charstring - no comments
⍝ ← represented array or space
⍝ Phil 2018-09-12 11.16
 }
