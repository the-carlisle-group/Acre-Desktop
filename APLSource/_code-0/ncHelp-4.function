 ncHelp←{⍺←⊢
     z←⎕EX'temp'
     z←{
         ⍵.GeneralArray←⍳2 3
         ⍵.ListOfStrings←↓⍪⎕D
         ⍵.CharacterMatrix←⎕FMT ⎕D
         ⍵.CharacterString←⎕D
         ⍵.DirectFunction←{⎕D}
         ⍵.DirectOperator←{⍺⍺}
         z←⍵.⎕FX'r←TraditionalFunction w' ''
         z←⍵.⎕FX'r←(f TraditionalOperator)w' ''
         ⍵.ContainerNamespace←⎕NS''
         ⍵.ScriptedNamespace←⎕FIX':namespace' ':endnamespace'
         ⍵.Class←⎕FIX':class' ':endclass'
         ⍵.Interface←⎕FIX':interface' ':endinterface'
         ⍵.DerivedFunction←+.×.-
         ⍵.MixedArray←1 '2' 3 '4' 5 '6' 7 '8' 9 '0'
         ⍵.NumericArray←2 3⍴⍳9
         ⍵.MissingItem←''
         ⍵.GuiObject←⎕NEW'Form'(⊂'Visible' 0)
         ⍵.ClassInstance←⎕NEW ⎕FIX':Class' ':EndClass'
         ⍵
     }⍎nn←'temp'⎕NS''
     nms←~∘' '¨↓temp.⎕NL⍳11
     r←UT.xc¨nms
     z←temp.⎕EX'MissingItem'
     r,(nameClass,∘⍪⎕NC)(nn,'.')∘,¨nms
⍝
⍝ Phil 2018-05-15 14.07
 }
