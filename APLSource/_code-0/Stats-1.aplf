 Stats←{dat←⍺
     space←⍎dat.nspath ⎕NS''
     start←DT.dt2fm⊃s t←2⍴dat.loadtime
     time←~∘' ',{(×⍵)⌿1⌽⍕'ms',⍪⍵}0 60⊤⌈--/24×60×60×DT.dt2dd s t
     s←b/⊃s b←{s _ 0 ssp⊃n s←n ⍵/¨⍨⊂(0<⎕NC)n←UT.df¨⍵}(∪⊢,UT.nt)space
     sub←,+/s.(≢⎕NL)2.1       ⍝ arrays
     sub,←+/s.(≢⎕NL)⊂3.1 3.2  ⍝ functions
     sub,←+/s.(≢⎕NL)4         ⍝ operators
     sub,←+/b=0               ⍝ scripts
     sub,←+/s.(≢⎕NL)9.4       ⍝ classes
     sub,←+/s.(≢⎕NL)9.5       ⍝ interfaces
     h s n←{1,(+/,≢){⍵.Size}folderObj dat.changes}⍣dat.hist⊢0 0 0
     hist←{(⊃'B'UT.mn s),MS.FP,n UT.pl MS.FL}⍣(×n)⊢MS.FQ,h⊃MS.GB MS.GC
     tot←(⍕+/sub),MS.FP,(+/b)UT.pl MS.FJ
     nms←MS.FG MS.FH''MS.FA MS.FB MS.FC MS.FD MS.FE MS.FF''MS.FI MS.FO
     UT.bx nms,⍪start time'',sub,''tot hist
                                        ⍝
⍝.--------------------------------------.
⍝| When opened:     2018-04-04 22:08:36 |
⍝| Time to load:                     2s |
⍝|                                      |
⍝| Variables                          4 |
⍝| Functions                        354 |
⍝| Operators                         81 |
⍝| Scripts                            2 |
⍝| Classes                            4 |
⍝| Interfaces                         0 |
⍝|                                      |
⍝| Total items:     445 in 9 namespaces |
⍝| Change History:     258kB in 9 files |
⍝'--------------------------------------'

⍝ Phil 2018-04-04 22.48
 }
