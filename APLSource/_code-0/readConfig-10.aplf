 readConfig←{cnf←⍵
     rl←Cc⊢ru←{⍵.⎕NL-2}res←configFields''
     0::res⊣logMsg UT.bx MS.DB,cnf
     cnf←{_NEXISTS ⍵:⍵          ⍝ new name 2018-11-12
         (⊃'/'UT.pn ⍵),MS.HM    ⍝ try old name
     }cnf
     ~_NEXISTS cnf:res
     d←nout _NGET cnf                             ⍝ lose blank rows
     r t←0{'⍝'=z←⊃~∘' '⊃⍵:(1+⍺)∇ 1↓⍵ ⋄ ⍺ z}d      ⍝ lose comment rows
     cl←Cc⊢cu←{⍵.⎕NL-2}cfg←t{                     ⍝ actuals
         ⍺='{':7159⌶1↓⊃,/(⎕UCS 13),¨⍵                 ⍝ JSON NOT v14.0!!!
         ⍺=':':0 ⎕FIX ⍵                               ⍝ APLscript
         ⍺='[':PL.decode ⍵                            ⍝ APLAN
         ⎕NS''                                        ⍝ none of the above
     }r↓d
     null⊢int←rl∩cl:res                           ⍝ supplied
     ⊃res(ru[rl⍳int])_is_ cfg.⍎⍕cu[cl⍳int] ⍝ merge
⍝ ⍵ config filename
⍝ config is APLAN or JSON or deprecated APLScript
⍝ result is namespace with all and only expected fields
⍝   with "proper" case restored
⍝ Phil 2018-11-12 10.05
 }
