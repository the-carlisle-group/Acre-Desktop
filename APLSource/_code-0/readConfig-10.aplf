 readConfig←{⍺←⊢ ⋄ cnf←⍵
     rl←Cc⊢ru←{⍵.⎕NL-2}dft←configFields if(0=⊢)⍺⊣0
     err←(11 ⎕SIGNAL⍨,)
     0::(MS.DB err cnf)                      ⍝ Problem reading
     cnf←{_NEXISTS ⍵:⍵                       ⍝ new name 2018-11-12
         (⊃'/'UT.pn ⍵),MS.HM                 ⍝ try old name
     }cnf
     ~_NEXISTS cnf:dft
     d←nout _NGET cnf                        ⍝ lose blank rows
     r t←0{'⍝'=z←⊃~∘' '⊃⍵:(1+⍺)∇ 1↓⍵ ⋄ ⍺ z}d ⍝ lose comment rows
     cl←Cc⊢cu←{⍵.⎕NL-2}cfg←t{                ⍝ actuals

         ⍺=':':0 ⎕FIX ⍵                      ⍝ APLscript
         ⍺='[':PL.decode ⍵                   ⍝ APLAN
         ⍺='{':_JSON ⍵                       ⍝ JSON
         MS.DU err cnf                       ⍝ Cannot ascertain encoding

     }r↓d
     z←dft('⎕io',cu[cl⍳exc])_is_ cfg.⍎⍕0,cu[cl⍳exc←cl~rl] ⍝ custom to dft
     0⊃dft('⎕io',ru[rl⍳int])_is_ cfg.⍎⍕0,cu[cl⍳int←rl∩cl] ⍝ supplied to dft
⍝ [⍺] default space
⍝ ⍵ config filename
⍝ config is APLAN or JSON or APLScript
⍝ result is namespace including all expected fields
⍝    having "proper" case restored
⍝ Phil 2019-08-15 23.02 5.2.0+239
 }
