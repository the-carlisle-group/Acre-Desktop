 _NPARTS←{⍺←⊢
     15≤⊃SV.aplver:⊢⍺ ⎕NPARTS ⍵

     ch←{('/',⍵)[('\',⍵)⍳⍵]}
     p f←'/'((-⍳⍨∘⌽)(↓_↑)⊢)((¯2×'/.'≡¯2↑⊢)↓⊢){
         {⍵↓⍨-+/∧\(⌽⍵)∊'/.'}⍣('.'=⊢/⍵)⊢⍵}ch ⍵
     f e←'.'((¯1-⍳⍨∘⌽)(↓_↑)⊢)∘⊃⍣('.'∊1↓f)⊢f''
     0=⍺⊣0:p f e

     p←(⊢,'/'~⊢/)ch FS.GetAbsolutePathName⊂p
     p f e

⍝  - see setEmulators
⍝ ⍺=0:
⍝   'path\file.ext'      → 'path\' 'file' '.ext'
⍝   'path\file.more.ext' → 'path\' 'file.more' '.ext'
⍝   'path\.file'         → 'path\' '.file' ''
⍝   'path\file'          → 'path\' 'file' ''
⍝
⍝ ⍺=1:
⍝   'path/file.ext'      → '[drive:]/fullpath/' 'file' '.ext'
⍝ Phil 2019-02-15 13.32
 }
