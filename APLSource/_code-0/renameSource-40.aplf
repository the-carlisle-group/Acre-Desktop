 renameSource←{dat←⍺ ⋄ path fqn←⍵
     path fqn/⍨←⊂~((-⍴MS.AR)↑¨path)∊⊂MS.AR ⍝ not .raw
     ren←(⊢(⌿⍨)≢/)path,⍪dat getSourceFilenames fqn
     z←{0}_MKDIR/3,⍪∪((-'/'⍳⍨⌽)↓⊢)¨,ren
     msg←{
         t←⊃{~∘⍵-1+⍳1+≢⍵}⎕NNUMS
         0::⊃,/MS.BM'"'⍺'"'MS.EZ'"'⍵'"'⊣⎕NUNTIE t
         {0}⎕NUNTIE ⍵ ⎕NRENAME ⍺ ⎕NTIE t
     }/ren
     {0}logMsg msg~0
⍝ ⍺ dat
⍝ ⍵ paths fqns
⍝   if source filename for fqn ≢ path then rename path
⍝ NOT IF path is currently a MS.AR ".raw"
⍝ - that nameClass will call MS.AA ".apla"
⍝ - should only occur in OpenProject
⍝ ← 0 - having messaged any failures
⍝ Phil 2020-03-28 13:29 8.0.0+310
 }
