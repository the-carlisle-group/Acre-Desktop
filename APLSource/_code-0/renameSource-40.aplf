 renameSource←{dat←⍺ ⋄ path fqn←⍵
     path fqn/⍨←⊂~((1⊃px)¨path)∊MS.AR MS.AD MS.MD ⍝ not .binary .dyalog .md
     ren←(⊢(⌿⍨)≢/)path,⍪dat getSourceFilenames fqn
     z←{0}_MKDIR/3,⍪∪((-'/'⍳⍨⌽)↓⊢)¨,ren
     msg←{
         t←⊃{~∘⍵-1+⍳1+≢⍵}⎕NNUMS
         0::⊃,/MS.BM'"'⍺'"'MS.EZ'"'⍵'"'⊣⎕NUNTIE t
         {0}⎕NUNTIE ⍵ ⎕NRENAME ⍺ ⎕NTIE t
     }/ren
     {0}logMsg msg~0
⍝ ⍺ dat
⍝ ⍵ paths fqns
⍝   if source filename for fqn ≢ path then rename path
⍝ unless path is currently ".binary" ".dyalog" ".md"
⍝ - that nameClass will call MS.AA ".apla"
⍝ - should only occur in OpenProject
⍝ ← 0 - having messaged any failures
⍝ Phil 2021-03-03 22:54 8.2.1+337
 }
