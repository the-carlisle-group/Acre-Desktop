 userCmdRun←{
     (new method arg type space path wsid name)←⍵
     ⎕THIS.OPT←⎕NS arg.(⎕NS'qwertyuiopasdfghjklzxcvbnm'⎕NL 2.1)
     z←Start⍣new⊢path wsid name              ⍝ once
     SV.debug←1∊⎕SE.SALTUtils.DEBUG          ⍝ OK?
     SV.space←space,'.'                      ⍝ whence called
     SV.errmsgs←''                           ⍝ new each cmd
     SV.arguments←,⊂arg.Arguments            ⍝ ditto
     z←openProjects''                        ⍝ still open?
     (item proj share)←'i' 'p' 's'=⊃type     ⍝ see cmdDefs
⍝         ↓↓↓↓↓
     res←method{
         item:⍺(⍎⍺)itemMethod ⍵              ⍝ items of arg are names
         proj:⍺(⍎⍺)projMethod ⍵              ⍝ first item is project
         share:⍺(⍎⍺)sharedMethod ⍵           ⍝ arg is specific to method
     }arg.Arguments
     z←⎕SIGNAL/(,/(⎕UCS 13),¨SV.errmsgs),11/⍨×≢SV.errmsgs
     res
⍝ ⍵ new; method; arg ns; type of fn; whence called; whence loaded; system.
⍝ Phil 2018-01-28 00.43
 }
