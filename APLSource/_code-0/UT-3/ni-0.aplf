 ni←{⍺←⊢
     ⍵≡⍺ ⍵:∇/⍵ ⋄ {⍵._Inst}{
         (⍵._Inst.⍎⍺,'←⍵∘{0::⎕SIGNAL/⌽⎕EN,⎕DM ⋄ ⍺ ⍺._Class.',⍺,'⍵}')/⍵
     }/⍵,⍺{⍵⊣⍵.(_Class _Inst)←⍺,#.⎕NS''}#.⎕NS''
⍝ ⍵ (class)(methods)
⍝ ← instance
⍝ class: space having at least the functions named in methods.
⍝ methods: names of explicit dyadic functions in class.
⍝ instance: space with derived methods that call the class
⍝           methods that share data in their unnamed space
⍝           left argument that refs instance and class.
⍝ .-class-----. .-instance-. .-------------.
⍝ | fns, data | | methods  | | data  class |
⍝ |   methods | '--↓-------' | instance ↓  |
⍝ '-----↓--↑--'    |   ↑     '--↓-------|--'
⍝    ↑  |  |       |   |        |   ↑   |
⍝    |  |  '-------'   '--------'   |   |
⍝    |  |                           |   |
⍝    |  '---------------------------'   |
⍝    |                                  |
⍝    '----------------------------------'
⍝ Phil 2019-12-16 10:56 6.0.2+287
 }
