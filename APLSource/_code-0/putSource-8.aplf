 putSource←{dat←⍺
     null⊢fqn type value←,∘⊂¨if(≡⊃)⍵:⍵
     src tgt←dat.(nspath source)
     path←dat getSourceFilenames fqn             ⍝ names .binary & .md as .apla & .charlist
     (raw/path)←,∘MS.AR¨(-⍴(MS.AZ⍳⊂MS.AA)⊃MS.AY)∘↓¨path/⍨raw←type∊⊂MS.AR
     old←×≢⊃_NINFO tgt,'*'                       ⍝ nothing in APLSource
     z←_MKDIR/3,⍪∪((-'/'⍳⍨⌽)↓⊢)¨path             ⍝ now there is
     z←{fn←{⊃⊃_NINFO(UT.sp ⍵),MS.AX}¨path
         bin←raw<((-⍴MS.AR)↑¨fn)∊⊂MS.AR          ⍝ .binary file exists
         (bin/value)←⊢∘⍎/0,⍪bin/fqn              ⍝ bin value, type & path
         (bin/type)←⊂MS.AR                       ⍝ - from .apla to .binary
         (bin/path)←bin/fn                       ⍝
         raw∨←bin
         md←(type∊⊂MS.AL)∧((-⍴MS.MD)↑¨fn)∊⊂MS.MD ⍝ .md file exists
         (md/type)←⊂MS.MD                        ⍝ md type & path
         (md/path)←md/fn                         ⍝ - from .charlist to .md
         dat renameSource(~nil fn)∘/¨fn fqn}⍣old⊢0
     (b/path)←{⍺ UT.re MS.AX ⍵}⌿↑path type/¨⍨⊂b←(1∊MS.AX⍷⊢)¨path
     z←putRaw/raw⌿value,⍪path
     z←{(,⊂if≡⍺)_NPUT ⍵}/(~raw)⌿value,⍪path
     fqn type value

⍝ ⍺ dat
⍝ ⍵ FQNs type values
⍝ ← ⍵ same - having written to sourcetree
⍝   write to source tree files
⍝ Phil 2021-03-16 18:36 8.2.1+338
 }
