 buildWsFromSource←{
     (⎕THIS #).(⎕IO ⎕ML ⎕WX)←⊂0 0 3 ⋄ LF CR BOM←⎕UCS 10 13 65279
     path←{⍵/⍨⌽∨\⌽⍵∊'/\'}⎕WSID
     src←path,'APLSource/'
     cde←⎕CMD'dir "',src,'_code-0/" /S'
     dat←(⊢(/⍨)1<≢¨)(⊢⊂⍨' '=⊃¨)(⊢(/⍨)0='<'∊¨⊢)~∘∊⍨cde
     fns dir←⊢∘⊃¨\⌽↓⍉↑1(↑{⍺ ⍵}↓)¨dat
     drop←{+/⍺≥+\2</0,' '≠⍵}
     dir⊢←(2 drop⊃dir)↓¨dir
     len←+/∨\⌽(⊃dir)∊'/\'
     fns⊢←(3 drop⊃⊃fns)↓¨¨fns
     read←((⎕NUNTIE⊢(⎕NREAD⊢,80,⎕NSIZE))0 ⎕NTIE⍨,)
     tran←{CR LF(1↓¨∊⍨⊂⊢)CR,{⍵~BOM,LF/⍨CR∊⍵}'UTF-8'⎕UCS ⎕UCS ⍵}
     get←{⍵↓⍨-0∊⍴⊃⌽⍵}tran∘read
     data←dir{get∘((⍺,'/')∘,)¨⍵}¨fns
     ns←(⍎''⎕NS⍨⊢)¨{{('..',⍵)[('\/',⍵)⍳⍵]}~∘('-',⎕D)⊢'#.acre.',len↓⍵}¨dir
     fns←ns.(⎕FX¨)data
     #.⎕LX←'#.acre._code.Install'''''
     local←(2 ⎕NQ'.' 'GetEnvironment' 'localappdata'),'\acre\'
     ver←local,'acre',{⍵/⍨∧\2>+\⍵='.'}1⊃'.'⎕WG'APLVersion'
     z←⎕CMD'md "',ver,'"'
     #.⎕WSID←ver,'\acre.dws'
     ⎕WSID⊣0 ⎕SAVE #.⎕WSID⊣⎕EX⊃⎕XSI
⍝ This ws, stored in and loaded from the acre development folder
⍝ as "build.dws", creates the intallation ws, that's copied into
⍝ ⎕SE each session, in whatever Dyalog version ≥14.0 into which
⍝ it's loaded. It saves it as e.g. "/acre/acre16.0/acre.dws"
⍝ in whatever is returned by
⍝   2 ⎕NQ'.' 'GetEnvironment' 'localappdata'
⍝ whence it can be )LOADed to associate the user commands with that
⍝ folder or copied to wherever the user requires acre installation
⍝ to reside and )LOADed thence.
⍝ Phil 2019-01-30 14.46
 }
