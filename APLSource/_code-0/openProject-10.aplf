 openProject←{⍺←⊢ ⋄ top←MS.DP≡⍺⊣0
     trk dep proj←⍵
     null⊢src←{getFolder'/'}if null⊃src tgt←2↑proj:0
     (dat←⎕NS'').(changed folder loadtime)←''(fPath src,'/'),DT.ts2dt ⎕TS
     dat.(config source code changes latest)←{
         ⍵}dat.folder∘,¨MS.HO MS.GY,MS.NB∘,¨''MS.EU MS.HE
     ~∨/2⍴src cnf cde←_NEXISTS¨dat.(source config changes):{
         0}logMsg MS.DG,' ',¯4 tPath dat.folder
     cfg←(dft←configFields'')readConfig dat.config ⍝ #171 SV.config
     z←'dat'⎕NS lcNs cfg.⎕NS dft.⎕NL 2 ⍝ reserved to dat
     ~((1=≢)∨0∘ssp∨0=⎕NC)dat.nspath←dat projPath tgt:{0}logMsg MS.DC
     ~dat openLoad trk dep:0 ⍝ load subsids

     z←logMsg(MS.ER,MS.EY),(¯4 tPath dat.folder),MS.EZ,dat.nspath
     z←⎕EX''if missing if(≢⍴){# UT.nl⍳10}if≢dat.nspath
     space←⍎dat.nspath #.⎕NS''⊣openProjects''
⍝   dat.(hist case vars)←On¨dat.(keephistory casecode variables)
     dat.(hist case)←On¨dat.(keephistory casecode)
⍝   cfg.(KeepHistory CaseCode Variables)←dat.(⊂hist case vars)⌷MS.GB MS.GA
     cfg.(KeepHistory CaseCode)←dat.(⊂hist case)⌷MS.GB MS.GA
     dat.cfg←PL.encode cfg⊣cfg.ProjectFolder←dat.folder
     z←space MS.FV _is_ PL.decode dat.cfg ⍝ ←↑ no JSON pre 14.1
     z←deleteFolder⍣(cde>dat.hist)⊢dat.code  ⍝ turned off
     z←_MKDIR/3,⍪dat.(source changes)/⍨(~src),on←cde<dat.hist
     z←dat∘putTime if⊢on                     ⍝ turned on
     z←MS.NB∘_NPUT if(~_NEXISTS)dat.folder,MS.FM  ⍝ git specific -
     z←dat getSource''
     dat.aftersave←{⍵.aftersave}(z.(⊃⎕NC'aftersave')⍳2)⊃z←dat SV
     dat.changed←⊃dat changes''
     z←space∘{0::⊢logMsg UT.bx MS.DN,MS.DP,⍵
         ⊢⍺.⍎UT.ep ⍵}if(trk∧top>null)dat.startup ⍝ trk?
     dat⊣dat.loadtime,←DT.ts2dt ⎕TS
⍝ [⍺] trk dep
⍝ ⍵ trk dep (folder [space])
⍝ ← dat or 0 - see OpenProject LoadProject CreateProject
⍝ Phil 2020-02-25 07:05 7.0.0+305
 }
