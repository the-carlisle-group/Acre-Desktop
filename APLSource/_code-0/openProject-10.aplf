 openProject←{⍺←⊢ ⋄ top←1≡⍺ 1 ⋄ dflt←⍺⊣1 1
     trk dep←dflt∧2⍴z z proj←⍵
     null⊢src←{getFolder'/'}if null⊃src tgt←2↑proj:0
     (dat←⎕NS'').(changed folder loadtime)←''(fPath src,'/'),DT.ts2dt ⎕TS
     dat.(config source code changes latest)←{
         ⍵}dat.folder∘,¨MS.HO MS.GY,MS.NB∘,¨''MS.EU MS.HE
     ~∨/2⍴src cnf cde←_NEXISTS¨dat.(source config changes):{
         0}logMsg MS.DG,' ',¯4 tPath dat.folder
     z←'dat'⎕NS lcNs⊢cfg←readConfig dat.config ⍝ see configFields
     nspath←{'/'UT.nn ¯1↓dat.folder}if null dat.projectspace if null tgt
     ~((1=≢)∨0∘ssp∨0=⎕NC)dat.nspath←⊃prefSpace nspath:{
         0}logMsg MS.DC
     ~dat openLoad trk dep:0 ⍝ load subsids

     z←logMsg(MS.ER,MS.EY),(¯4 tPath dat.folder),MS.EZ,dat.nspath
     empty←{0=⎕NC ⍵:⍵ ⋄ ⍵/⍨~∧/ref↓(⍎⍵)UT.nl⍳10}
     z←⎕EX empty if(≢⍴){# UT.nl⍳10}if≢dat.nspath
     space←⍎dat.nspath #.⎕NS''⊣openProjects''
     cfg.KeepHistory←MS.GB MS.GA⊃⍨dat.hist←nOff dat.keephistory
     cfg.CaseCode←MS.GB MS.GA⊃⍨dat.case←≢/Cc dat.casecode MS.GB
     dat.cfg←⎕NS cfg⊣space MS.FV _is_ cfg⊣cfg.ProjectFolder←dat.folder
     z←deleteFolder⍣(cde>dat.hist)⊢dat.code  ⍝ turned off
     z←_MKDIR/3,⍪dat.(source changes)/⍨(~src),on←cde<dat.hist
     z←dat∘putTime if⊢on                     ⍝ turned on
     z←MS.NB∘_NPUT if(~_NEXISTS)dat.folder,MS.FM  ⍝ git specific -
     z←dat getSource''
     dat.changed←⊃dat changes''
     z←space∘{0::⊢logMsg UT.bx MS.DN,MS.DP,⍵
         ⊢⍺.⍎UT.ep ⍵}if(trk∧top>null)dat.startup ⍝ trk?
     dat⊣dat.loadtime,←DT.ts2dt ⎕TS
⍝ [⍺] trk dep
⍝ ⍵ trk dep (folder [space])
⍝ ← dat or 0 - see OpenProject LoadProject CreateProject
⍝ Phil 2019-01-17 16.32
 }
