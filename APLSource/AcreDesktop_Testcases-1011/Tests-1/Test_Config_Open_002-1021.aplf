 R←Test_Config_Open_002(stopFlag batchFlag);⎕TRAP;projectPath;ref;projectName;configParms;result;list;projectName2;projectPath2;ref2;openProjects;noOfProjects;configFilename;bin;messages
⍝ Create a project and check whether `acre.config[Open]` is honored. (Single project, enclosed "Open")
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←T.∆Failed

 noOfProjects←⊃⍴∆RunUcmd'Projects'
 projectName←⊃⎕SI
 projectPath←F.EnforceSlash ∆PATH,'/',projectName
 #.⎕SHADOW projectName
 ref←⍎projectName #.⎕NS''
 ref.⎕FX'r←Hello' 'r←''World'''
 ∆ClearLog

 :Trap (~stopFlag)/0
     {}∆RunUcmd'Createproject "',projectPath,'" #.',projectName
 :Else
     →T.GoToTidyUp 1
 :EndTrap
 →T.GoToTidyUp 1≠≢messages←∆GetLog ⍬

 projectName2←(⊃⎕SI),'_2'
 projectPath2←F.EnforceSlash ∆PATH,'/',projectName2
 #.⎕SHADOW projectName2
 ref2←⍎projectName2 #.⎕NS''
 ref2.⎕FX'r←Hello' 'r←''Universe'''

 :Trap (~stopFlag)/0
     {}∆RunUcmd'Createproject "',projectPath2,'" #.',projectName2
 :Else
     →T.GoToTidyUp 1
 :EndTrap
 →T.GoToTidyUp 3≠≢messages←∆GetLog ⍬

 #.⎕EX¨projectName projectName2
 #.⎕SHADOW'Temp'
 (bin configFilename)←∆ReadConfigFile projectPath
 configParms←''
 configParms,←⊂'{'
 configParms,←⊂' "Open": "',projectPath2,' #.Temp",'
 configParms,←⊂' "ProjectSpace": "#.',projectName,'"'
 configParms,←⊂'}'
 configParms←,¨configParms
 A.WriteUtf8File configFilename configParms

 :Trap (~stopFlag)/0
     list←∆RunUcmd'OpenProject ',projectPath
 :Else
     →T.GoToTidyUp 1
 :EndTrap

 →T.GoToTidyUp 9=#.⎕NC projectName2
 →T.GoToTidyUp 0=#.⎕NC'Temp'
 →T.GoToTidyUp 3≠#.Temp.⎕NC'Hello'
 →T.GoToTidyUp'Universe'≢#.Temp.Hello
 →T.GoToTidyUp 9≠≢messages←∆GetLog ⍬
 →T.GoToTidyUp 2≠(⊃⍴∆RunUcmd'Projects')-noOfProjects

 R←T.∆OK

∆TidyUp:
 :Trap 0 ⋄ F.RmDir projectPath ⋄ :EndTrap
 :Trap 0 ⋄ F.RmDir projectPath2 ⋄ :EndTrap
 ∆ClearLog
 ∆RunUcmd'Close #.',projectName
 ∆RunUcmd'Close #.',projectName2
⍝Done
