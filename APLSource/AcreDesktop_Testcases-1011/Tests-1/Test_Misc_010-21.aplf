 R←Test_Misc_010(stopFlag batchFlag);⎕TRAP;projectName;projectPath;ref;result;DM
⍝ Change a function to an operator. Acre now takes care of this by performing a rename operation.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←T.∆Failed

 projectName←⊃⎕SI
 projectPath←∆PATH,'/',projectName
 #.⎕SHADOW projectName
 ref←⍎projectName #.⎕NS''
 ref.⎕FX'r←Hello dummy' 'r←42'
 ∆ClearLog

 :Trap (~stopFlag)/0
     result←∆RunUcmd'Createproject ',projectPath,' #.',projectName
 :Else
     →T.GoToTidyUp 1
 :EndTrap

 →T.GoToTidyUp result≢'#.',projectName
 →T.PassesIf 1=+/,'Hello.aplf'⍷↑'Recursive'F.ListFiles projectPath,'\APLSource\'
 ref.⎕FX'r←(fns Hello) dummy' 'r←42'
 result←∆RunUcmd'SetChanged #.',projectName,'.Hello'
 →T.GoToTidyUp(,⊂'#.',projectName,'.Hello')≢result
 result←∆RunUcmd'Close #.',projectName
 →T.GoToTidyUp 1≠result
 #.⎕EX projectName
 #.⎕SHADOW projectName
 ∆ClearLog

 result←∆RunUcmd'OpenProject ',projectPath
 →T.PassesIf 1=+/,'Hello.aplo'⍷↑'Recursive'F.ListFiles projectPath,'\APLSource\'
 →T.PassesIf 0=+/,'Hello.aplf'⍷↑'Recursive'F.ListFiles projectPath,'\APLSource\'

 R←T.∆OK

∆TidyUp:
 F.RmDir projectPath
 ∆ClearLog
 ∆RunUcmd'Close #.',projectName
⍝Done
