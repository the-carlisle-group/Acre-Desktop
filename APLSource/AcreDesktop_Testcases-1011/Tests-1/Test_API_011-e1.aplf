 R←Test_API_011(stopFlag batchFlag);⎕TRAP;buff;result;F;path;config;CR
⍝ Exercise the API function `Config`.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←∆Failed

 F←##.FilesAndDirs
 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 #.TEMP.Foo←{42}

 path←{_←F.DeleteFile ⍵ ⋄ ¯4↓⍵}F.GetTempFilename''
 result←∆RunUcmd'CreateProject "',path,'" #.TEMP'
 Assert'#.TEMP'≡result

 config←'flat'##.APLTreeUtils.ReadUtf8File path,'/acre.config'
 CR←⎕UCS 13
 config←CR ⎕SE.APLTreeUtils.Split({⍵↑⍨¯1+⍵⍳'}'}config),',',CR,'  "ABC":"Foo",',CR,'  "DOB":"1954-10-02"',CR,'}'
 ##.APLTreeUtils.WriteUtf8File(path,'/acre.config')config

 {}∆RunUcmd'Close #.TEMP'
 result←∆RunUcmd'OpenProject "',path,'"' ⍝ ,' -'
 Assert'#.TEMP'≡result

 buff←⎕SE.acre.Config'#.TEMP'
 →GoToTidyUp'Foo'≢buff.ABC
 →GoToTidyUp'1954-10-02'≢buff.DOB

 R←∆OK

∆TidyUp:
 :Trap 0 ⋄ {}F.RmDir path ⋄ :EndTrap
 ∆RunUcmd'Close #.TEMP'
⍝Done
