 R←Test_Config_Open_005(stopFlag batchFlag);⎕TRAP;projectPath;bin;configFilename;dmx;configParms
⍝ Create a project and check whether an invalid config file makes acre signal an error (was once a bug)
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←∆Failed

 ∆ClearLog
 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 Assert' '=1↑0⍴⊃#.TEMP.⎕FX↑'r←Hello' 'a←''World'''
 projectPath←F.GetTempPath,⊃⎕SI
 F.RmDir projectPath

 :Trap (~stopFlag)/0
     {}∆RunUcmd'Createproject "',projectPath,'" #.TEMP'
 :Else
     →GoToTidyUp 1
 :EndTrap

 ∆RunUcmd'Close #.TEMP'
 #.⎕EX'TEMP'

 configFilename←2⊃∆ReadConfigFile projectPath
 configParms←''
 configParms,←⊂'{'
 configParms,←⊂'  "Open": ""'
 configParms,←⊂'  "ProjectSpace": "#.TEMP"'
 configParms,←⊂'  "StartUp" ""'               ⍝ Watch out: invalid syntax: missing `:`!
 configParms,←⊂'}'
 A.WriteUtf8File configFilename configParms

 :Trap 0
     {}∆RunUcmd'OpenProject ',projectPath
     →GoToTidyUp 1
 :Else
     dmx←⎕DMX
     →PassesIf dmx.EN∊11 911
     →PassesIf'Command Execution Failed: Problem reading'(1∊⍷)⊃dmx.DM
 :EndTrap

 R←∆OK

∆TidyUp:
 :Trap 0 ⋄ F.RmDir projectPath ⋄ :EndTrap
 ∆ClearLog
 ∆RunUcmd'Close #.TEMP'
⍝Done
