 R←Test_OpenProject_001(stopFlag batchFlag);⎕TRAP;projectName;projectPath;ref;result;code
⍝ Exercise `]OpenProject` with somehting that should not fix at all
⍝ Although acre will throw an error eventually it should open the main project and one of the two dependencies.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←∆Failed

⍝ Prepare the project
 projectName←(⊃⎕SI),'_Main'
 #.⎕SHADOW projectName
 projectPath←∆PATH,'/',projectName
 ref←⍎projectName #.⎕NS''

⍝ Create a namespace that holds the variable used to collect acre's messages for analyzing purposes
 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 #.TEMP.Log←''

⍝ Create the project
 :Trap (~stopFlag)/0
     result←∆RunUcmd'Createproject ',projectPath,' #.',projectName
 :Else
     →GoToTidyUp 1
 :EndTrap
 →GoToTidyUp result≢'#.',projectName

 ⍝ So far this project does not contain anything. We now create a file in the
 ⍝ APLSource\ folder that won't fix due a syntax problem.
 code←':Class Foo' '∇r←Hello' ':EndClass'
 ##.APLTreeUtils.WriteUtf8File(projectPath,'/APLSource/foo.class')code

 #.⎕EX projectName
 result←∆RunUcmd'Projects'
 →GoToTidyUp~(⊃⍴result)∊0 1             ⍝ acre itself might or might not be open by now.

 :Trap 911
     {}∆RunUcmd'OpenProject ',projectPath,' #.',projectName
     →GoToTidyUp 1
 :EndTrap
 →GoToTidyUp 9≠#.⎕NC projectName
 →GoToTidyUp'AcreConfig' 'quadVars'≢' '~¨⍨↓(#.⍎projectName).⎕NL⍳16

 R←∆OK

∆TidyUp:
 F.RmDir projectPath
⍝Done
