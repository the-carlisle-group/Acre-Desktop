 R←Test_CreateProject_003(stopFlag batchFlag);⎕TRAP;projectPath;ref;projectName;result;list;noOf
⍝ Create a project which has just a namespace script.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←T._Failed

 projectName←⊃⎕SI
 projectPath←∆PATH,'/',projectName
 #.⎕SHADOW projectName
 ref←⍎projectName #.⎕NS''
 ref.⎕FIX':Namespace Foo' '∇r←Hello' 'r←''World''' '∇' ':EndNamespace'
 noOf←⊃⍴∆RunUcmd'Projects'
 ∆ClearLog

 :Trap (~stopFlag)/0
     result←∆RunUcmd'Createproject ',projectPath,' #.',projectName,' -casecode=on'
 :Else
     →T.GoToTidyUp 1
 :EndTrap

 →T.GoToTidyUp 0=F.IsDir projectPath,'/APLSource'
 →T.GoToTidyUp 0=F.IsFile projectPath,'/acre.config'
 →T.GoToTidyUp(F.EnforceSlash projectPath,'/APLSource/Foo-1.apln')≢F.EnforceSlash⊃⊃F.Dir projectPath,'/APLSource/*'

 R←T._OK

∆TidyUp:
 F.RmDir projectPath
 ∆ClearLog
 ∆RunUcmd'CloseProject #.',projectName
⍝Done
