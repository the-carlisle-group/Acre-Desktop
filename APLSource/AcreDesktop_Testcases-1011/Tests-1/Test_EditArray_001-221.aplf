 R←Test_EditArray_001(stopFlag batchFlag);⎕TRAP;rc;_;acreDir;log;result;txt;list;filename
⍝ Make changes to a text vector via ]editarray and check whether acre took appropriate action.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 :If 'Win'≢3↑1⊃'#'⎕WG'APLVersion'
     R←∆WindowsOnly
     :Return
 :ElseIf batchFlag
     R←∆NoBatchTest
     :Return
 :Else
     R←T.∆Failed
     #.⎕SHADOW'TEMP'
     'TEMP'#.⎕NS''
     #.TEMP.Hello←txt←'Please make a change and then press <escape> in this window'
     acreDir←F.GetTempPath,⊃⎕SI
     result←∆RunUcmd'CreateProject ',acreDir,' #.TEMP -casecode=on -variables=on -keephistory=on'
     →GoToTidyUp'#.TEMP'≢result
     result←∆RunUcmd'EditArray #.TEMP.Hello'
     'Variable was not changed?!'Assert #.TEMP.Hello≢txt
     ∆WaitAndProcess 0.2
     list←F.ListFiles acreDir,'\.acre\changes\'
     →GoToTidyUp 1≠≢list
     filename←2⊃##.APLTreeUtils.SplitPath⊃list
     →GoToTidyUp filename≢'Hello-1.charstring.change'
     R←T.∆OK
∆TidyUp:
     ⊣F.RmDir acreDir
 :EndIf
 ∆ClearLog
 ∆RunUcmd'Close #.TEMP'
⍝Done
