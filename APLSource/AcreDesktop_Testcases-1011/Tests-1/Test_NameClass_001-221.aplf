 R←Test_NameClass_001(stopFlag batchFlag);⎕TRAP;projectName;projectPath;ref;messages
⍝ Exercise `NameClass` function.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←T.∆Failed

 ∆ClearLog
 projectName←⊃⎕SI
 projectPath←∆PATH,'/',projectName
 #.⎕SHADOW projectName
 ref←⍎projectName #.⎕NS''
 ref.⎕FX'r←Hello' 'r←''World'''
 ref.⎕FX'Hello2←{''Universe''}'
 ref.vtv←'APL' 'Python' 'Ruby'
 ref.charmat←↑'APL' 'Python' 'Ruby'
 ref.charvector←'APL is great'
 ref.int←1 2 3
 ref.mat←?2 3⍴1200
 ref.nested←⊂⊂⊂1 2 3
 ref.empty1←⍬
 ref.empty2←''
 'container'ref.⎕NS''
 ref.⎕FIX,¨':Namespace Foo' '∇r←Hello3' 'r←''All the rest''' '∇' ':EndNamespace'
 ref.⎕FIX':Class Goo' '∇r←Hello4' ':Access Public Shared' 'r←''All the rest''' '∇' ':EndClass'
 ref.⎕FIX':Interface Boo' '∇r←Hello5' '∇' ':EndInterface'
 #.⎕SHADOW'AcreMessages'
 {}∆RunUcmd'Projects'  ⍝ Make sure that at least one acre command was executed before doing a `run`.
 →T.PassesIf 1=≢messages←∆GetLog ⍬

 →T.PassesIf'.aplf'≡⊃⎕SE.acre.Nameclass⊃⎕XSI
 →T.PassesIf'.apln'≡⊃⎕SE.acre.Nameclass'#.',projectName,'.Foo'
 →T.PassesIf'.aplc'≡⊃⎕SE.acre.Nameclass'#.',projectName,'.Goo'
 →T.PassesIf'.apli'≡⊃⎕SE.acre.Nameclass'#.',projectName,'.Boo'
 →T.PassesIf'.charlist'≡⊃⎕SE.acre.Nameclass'#.',projectName,'.vtv'
 →T.PassesIf'.charmat'≡⊃⎕SE.acre.Nameclass'#.',projectName,'.charmat'
 →T.PassesIf'.charstring'≡⊃⎕SE.acre.Nameclass'#.',projectName,'.charvector'
 →T.PassesIf'.array'≡⊃⎕SE.acre.Nameclass'#.',projectName,'.int'
 →T.PassesIf'.array'≡⊃⎕SE.acre.Nameclass'#.',projectName,'.mat'
 →T.PassesIf'.array'≡⊃⎕SE.acre.Nameclass'#.',projectName,'.nested'
 →T.PassesIf'.array'≡⊃⎕SE.acre.Nameclass'#.',projectName,'.empty1'
 →T.PassesIf'.charstring'≡⊃⎕SE.acre.Nameclass'#.',projectName,'.empty2'
 →T.PassesIf'.*'≡⊃⎕SE.acre.Nameclass'Certainly_unknown'
 →T.PassesIf'.apln' '.aplc' '.apli'≡⎕SE.acre.Nameclass(⊂'#.',projectName),¨'.Foo' '.Goo' '.Boo'
 →T.PassesIf 0∊⍴⊃⎕SE.acre.Nameclass'#.',projectName,'.container'
 →T.PassesIf'.*'≡⊃⎕SE.acre.Nameclass'#.DoesNotExist'

 R←T.∆OK
 ∆ClearLog
⍝Done
