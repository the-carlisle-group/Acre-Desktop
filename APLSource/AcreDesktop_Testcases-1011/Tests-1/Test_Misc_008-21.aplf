 R←Test_Misc_008(stopFlag batchFlag);⎕TRAP;projectName;projectPath;ref;result;sourceFilename;body
⍝ Exercise the `]Refresh` command.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←T.T._Failed

 projectName←⊃⎕SI
 projectPath←∆PATH,'/',projectName
 #.⎕SHADOW projectName
 ref←⍎projectName #.⎕NS''
 ref.⎕FX'r←Hello' 'r←''World'''
 ref.⎕FX'r←Hello2' 'r←''Universe'''
 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 #.TEMP.Log←''
 ∆ClearLog

 :Trap (~stopFlag)/0
     result←∆RunUcmd'Createproject ',projectPath,' #.',projectName
 :Else
     →T.GoToTidyUp 1
 :EndTrap
 →T.GoToTidyUp result≢'#.',projectName

 sourceFilename←⎕SE.acre.Sourcefile'#.',projectName,'.Hello'
 →T.GoToTidyUp 1≠≢sourceFilename
 sourceFilename←⊃sourceFilename
 body←##.APLTreeUtils.ReadUtf8File sourceFilename
 (2⊃body)←'r←''Galaxy'''
 ⎕DL 1  ⍝ Between "Create project" and a change made to the file we need a delay of at least one second so that acre's ]Refresh can spot the change.
 ##.APLTreeUtils.WriteUtf8File sourceFilename body
 result←∆RunUcmd'Refresh #.',projectName
 →T.GoToTidyUp 1≠⍴result                              ⍝ Should be a list of names of those objects that got refreshed?!
 →T.GoToTidyUp result≢,⊂'#.',projectName,'.Hello'
 →T.GoToTidyUp'Galaxy'≢⍎'#.',projectName,'.Hello'

 R←T.T._OK

∆TidyUp:
 F.RmDir projectPath
 ∆ClearLog
 ∆RunUcmd'Close #.',projectName
⍝Done
