 R←Test_CreateProject_009(stopFlag batchFlag);⎕TRAP;path;rc;en;more;projectPath;ref;projectName;result;list
⍝ Create a project from an empty namespace. Should inject `QuadVars`.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 projectName←⊃⎕SI
 projectPath←∆PATH,'/',projectName
 #.⎕SHADOW projectName
 ref←⍎projectName #.⎕NS''
 ∆ClearLog

 :Trap (~stopFlag)/0
     result←∆RunUcmd'Createproject ',projectPath,' #.',projectName,' -casecode=on'
 :Else
     →T.GoToTidyUp 1
 :EndTrap

 →T.GoToTidyUp 0=F.IsDir projectPath,'/APLSource'
 →T.GoToTidyUp 0=F.IsFile projectPath,'/acre.config'
 →T.GoToTidyUp 1≠≢list←⊃F.Dir projectPath,'/APLSource/*'

 R←T._OK

∆TidyUp:
 F.RmDir projectPath
 ∆ClearLog
 ∆RunUcmd'Close #.',projectName
⍝Done
