 R←Test_Misc_009(stopFlag batchFlag);⎕TRAP;projectName;projectPath;ref;result;list;noOf
⍝ Exercise the `]acre.Close` command.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←T.∆Failed

 noOf←≢∆RunUcmd'Projects'
 projectName←⊃⎕SI
 projectPath←∆PATH,'/',projectName
 #.⎕SHADOW projectName
 ref←⍎projectName #.⎕NS''
 ref.⎕FX'r←Hello' 'r←''World'''

 :Trap (~stopFlag)/0
     result←∆RunUcmd'Createproject ',projectPath,' #.',projectName
 :Else
     →GoToTidyUp 1
 :EndTrap
 →GoToTidyUp result≢'#.',projectName

 →T.PassesIf(noOf+1)=≢∆RunUcmd'Projects'
 result←∆RunUcmd'Close #.',projectName
 →T.PassesIf noOf=≢∆RunUcmd'Projects'

 R←T.∆OK

∆TidyUp:
 F.RmDir projectPath
 ∆ClearLog
 ∆RunUcmd'Close #.',projectName
⍝Done
