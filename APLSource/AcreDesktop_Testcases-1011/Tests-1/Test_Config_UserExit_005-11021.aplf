 R←Test_Config_UserExit_005(stopFlag batchFlag);⎕TRAP;configFilename;bin;buff;list;messages;projectPath
⍝ Create a project and check whether the user's exit function is called correctly without returning a result.
⍝ This is the user exit that allows the execution of any source code management command like Git's "Commit".
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 →0,R←∆Inactive   ⍝ At the moment a user exit function that does not return a result would freeze the APL session.

 :If batchFlag
     →0,R←∆NoBatchTest
 :EndIf

 R←∆Failed

 ∆ClearLog
 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 #.TEMP.∆FLAG←0
 Assert' '=1↑0⍴⊃#.TEMP.⎕FX↑'x UserExit y' '⍝ No result!' '#.TEMP.∆FLAG←1'
 Assert' '=1↑0⍴⊃#.TEMP.⎕FX↑'r←Hello' '⍝ Change the function and then press <escape>'
 projectPath←F.GetTempPath,⊃⎕SI
 F.RmDir projectPath

 :Trap (~stopFlag)/0
     {}'-variables=on'∆RunUcmd'Createproject "',projectPath,'" #.TEMP'
 :Else
     →GoToTidyUp 1
 :EndTrap

 ∆RunUcmd'Close #.TEMP'
 #.⎕EX'TEMP'

 (bin configFilename)←∆ReadConfigFile projectPath
 buff←''
 buff,←⊂':Namespace'
 buff,←⊂'   Open←'''''
 buff,←⊂'   ProjectSpace←''#.TEMP'''
 buff,←⊂'   SCMexit←''#.TEMP.UserExit'''
 buff,←⊂':EndNamespace'
 A.WriteUtf8File configFilename buff

 :Trap (~stopFlag)/0
     list←∆RunUcmd'OpenProject ',projectPath
 :Else
     →GoToTidyUp 1
 :EndTrap

 #.TEMP.⎕ED'Hello'
 →GoToTidyUp #.TEMP.∆FLAG≠1

 R←∆OK

∆TidyUp:
 :Trap 0 ⋄ F.RmDir projectPath ⋄ :EndTrap
 ∆ClearLog
 ∆RunUcmd'Close #.TEMP'
⍝Done
