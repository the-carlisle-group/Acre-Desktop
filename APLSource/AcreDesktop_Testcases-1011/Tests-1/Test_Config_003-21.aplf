 R←Test_Config_003(stopFlag batchFlag);⎕TRAP;projectPath;ref;projectName;configParms;result;list;bin;configFilename
⍝ Create a project and check whether `acre.config[ProjectSpace]` is honored.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←T._Failed

 projectName←⊃⎕SI
 projectPath←∆PATH,'/',projectName
 #.⎕SHADOW projectName
 ref←⍎projectName #.⎕NS''
 ref.⎕FX'r←Hello' 'r←''World'''
 ∆ClearLog

 :Trap (~stopFlag)/0
     result←∆RunUcmd'Createproject "',projectPath,'" #.',projectName
 :Else
     →T.GoToTidyUp 1
 :EndTrap

 #.⎕EX projectName
 #.⎕SHADOW'Temp'
 (bin configFilename)←∆ReadConfigFile projectPath
 configParms←''
 configParms,←⊂'{'
 configParms,←⊂' "ProjectSpace": "#.Temp"'
 configParms,←⊂'}'
 configParms←,¨configParms
 A.WriteUtf8File configFilename configParms

 :Trap (~stopFlag)/0
     list←∆RunUcmd'Projects '
 :Else
     →T.GoToTidyUp 1
 :EndTrap

 →T.GoToTidyUp'#.Temp'≢∆RunUcmd'OpenProject ',projectPath
 →T.GoToTidyUp 1≠(⊃⍴∆RunUcmd'Projects')-⊃⍴list
 →T.GoToTidyUp'World'≢#.Temp.Hello

 R←T._OK

∆TidyUp:
 F.RmDir projectPath
 ∆ClearLog
 ∆RunUcmd'Close #.',projectName
⍝Done
