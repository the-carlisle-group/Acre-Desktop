 R←Test_Config_UserExit_006(stopFlag batchFlag);⎕TRAP;configFilename;bin;configParms;list;messages;projectPath
⍝ A user exit function is supposed to by dyadic. If it monadic it won't be called but an error is logged.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←T.∆Failed

 :If batchFlag
     →0,R←∆NoBatchTest
 :EndIf

 ∆ClearLog
 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 #.TEMP.∆FLAG←0
 Assert' '=1↑0⍴⊃#.TEMP.⎕FX↑'r←UserExit arg' '⍝ Monadic!' 'r←#.TEMP.∆FLAG←1'
 Assert' '=1↑0⍴⊃#.TEMP.⎕FX↑'r←Hello' '⍝ Change the function and then press <escape>'
 projectPath←F.GetTempPath,⊃⎕SI
 F.RmDir projectPath

 :Trap (~stopFlag)/0
     {}'-variables=on'∆RunUcmd'Createproject "',projectPath,'" #.TEMP'
 :Else
     →T.GoToTidyUp 1
 :EndTrap

 ∆RunUcmd'Close #.TEMP'
 #.⎕EX'TEMP'

 (bin configFilename)←∆ReadConfigFile projectPath
 configParms←''
 configParms,←⊂'{'
 configParms,←⊂' "Open": "",'
 configParms,←⊂' "ProjectSpace": "#.TEMP",'
 configParms,←⊂' "AfterSave": "#.TEMP.UserExit",'
 configParms,←⊂' "Foo": 1'
 configParms,←⊂'}'
 configParms←,¨configParms
 A.WriteUtf8File configFilename configParms

 :Trap (~stopFlag)/0
     list←∆RunUcmd'OpenProject ',projectPath
 :Else
     →T.GoToTidyUp 1
 :EndTrap

 #.TEMP.⎕ED'Hello'
 →T.GoToTidyUp #.TEMP.∆FLAG≠0

 R←T.∆OK

∆TidyUp:
 :Trap 0 ⋄ F.RmDir projectPath ⋄ :EndTrap
 ∆ClearLog
 ∆RunUcmd'Close #.TEMP'
⍝Done
