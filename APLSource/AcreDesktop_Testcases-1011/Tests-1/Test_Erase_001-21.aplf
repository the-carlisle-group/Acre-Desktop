 R←Test_Erase_001(stopFlag batchFlag);⎕TRAP;projectName;projectPath;ref;result;projects;path;list
⍝ Exercise `Erase` and `Restore` on functions.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←T.∆Failed

 projectName←⊃⎕SI
 projectPath←∆PATH,'/',projectName
 #.⎕SHADOW projectName
 ref←⍎projectName #.⎕NS''
 ref.⎕FX'r←Hello' 'r←''World'''
 ref.⎕FX'r←Hello2' 'r←''World2'''
 ∆ClearLog

 :Trap (~stopFlag)/0
     result←∆RunUcmd'Createproject ',projectPath,' #.',projectName,' -keephistory=on'
 :Else
     →GoToTidyUp 1
 :EndTrap

 →GoToTidyUp result≢'#.',⊃⎕SI
 projects←∆RunUcmd'Projects'
 path←(⊃projects[(projects[;1]≡¨⊂'#.',projectName)⍳1;2]),'APLSource/'
 list←⊃F.Dir path,'*'
 →GoToTidyUp 3≠⍴list  ⍝ Quad-Vars script is added automatically
 result←∆RunUcmd'Erase #.',projectName,'.Hello'
 →GoToTidyUp result≢,⊂'#.',projectName,'.Hello'
 →GoToTidyUp 0≠ref.⎕NC'Hello'
 →GoToTidyUp 3≠ref.⎕NC'Hello2'
 result←∆RunUcmd'Restore #.',projectName,'.Hello'
 →GoToTidyUp result≢,⊂'#.',projectName,'.Hello'
 →GoToTidyUp 3≠ref.⎕NC'Hello'
 →GoToTidyUp 3≠ref.⎕NC'Hello2'
 result←∆RunUcmd'Erase #.',projectName,'.Hello #.',projectName,'.Hello2'
 →GoToTidyUp result≢('#.',projectName,'.Hello')('#.',projectName,'.Hello2')
 →GoToTidyUp 0∨.≠⊃∘ref.⎕NC¨'Hello' 'Hello2'
 →GoToTidyUp 1≠⍴⊃F.Dir path,'*'   ⍝ Quad-Vars script is added automatically
 path←(⊃projects[(projects[;1]≡¨⊂'#.',projectName)⍳1;2]),'.acre/changes/'
 list←⊃F.Dir path,'*'
 →GoToTidyUp 2≠⍴list
 →GoToTidyUp'delete' 'delete'≢A.Last¨list
 result←∆RunUcmd'Restore #.',projectName,'.Hello #.',projectName,'.Hello2'
 →GoToTidyUp 3≠ref.⎕NC'Hello'
 result←∆RunUcmd'Restore #.',projectName,'.Hello2'
 →GoToTidyUp 3∨.≠⊃∘ref.⎕NC¨'Hello' 'Hello2'
 path←(⊃projects[(projects[;1]≡¨⊂'#.',projectName)⍳1;2]),'APLSource/'
 →GoToTidyUp 3≠⍴⊃F.Dir path,'*'   ⍝ Quad-Vars script is added automatically
 path←(⊃projects[(projects[;1]≡¨⊂'#.',projectName)⍳1;2]),'.acre/changes/'
 list←⊃F.Dir path,'*'
 →GoToTidyUp 2≠⍴list
 →GoToTidyUp'change' 'change'≢A.Last¨list

 R←T.∆OK

∆TidyUp:
 F.RmDir projectPath
 ∆ClearLog
 ∆RunUcmd'Close #.',projectName
⍝Done
