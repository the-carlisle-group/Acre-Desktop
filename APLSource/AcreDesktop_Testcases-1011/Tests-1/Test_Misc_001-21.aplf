 R←Test_Misc_001(stopFlag batchFlag);⎕TRAP;projectName;projectPath;ref;result;log
⍝ Exercise the `Stats` command
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←T.T._Failed

 projectName←⊃⎕SI
 projectPath←∆PATH,'/',projectName
 #.⎕SHADOW projectName
 ref←⍎projectName #.⎕NS''
 ref.⎕FX'r←Hello' 'r←''World'''
 ∆ClearLog

 {}∆RunUcmd'CreateProject ',projectPath,' #.',projectName
 result←∆RunUcmd'Stats -all'
 →T.GoToTidyUp 2≠≢result
 →T.GoToTidyUp 2 2≢⊃∘⍴∘⍴¨,result

 result←∆RunUcmd'Stats #.',projectName
 →T.GoToTidyUp 2≠⍴⍴result

 result←∆RunUcmd'Stats'
 →T.GoToTidyUp 0≠≢result

 R←T.T._OK

∆TidyUp:
 :Trap 0 ⋄ {}F.RmDir projectPath ⋄ :EndTrap
 ∆ClearLog
 ∆RunUcmd'Close #.',projectName
⍝Done
