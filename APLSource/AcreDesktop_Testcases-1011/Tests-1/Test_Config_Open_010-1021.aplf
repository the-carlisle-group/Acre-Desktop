 R←Test_Config_Open_010(stopFlag batchFlag);⎕TRAP;projectPath;ref;projectName;configParms;list;projectName2;projectPath2;ref2;noOfProjects;bin;configFilename;messages
⍝ Create a project and check whether `Open` with `-track=off` is honored.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←T.∆Failed

 noOfProjects←⊃⍴∆RunUcmd'Projects'
 projectName←⊃⎕SI
 projectPath←F.EnforceSlash ∆PATH,'/',projectName
 #.⎕SHADOW projectName
 ref←⍎projectName #.⎕NS''
 ref.⎕FX'r←Hello' 'r←''World'''
 ∆ClearLog

 :Trap (~stopFlag)/0
     {}∆RunUcmd'Createproject ',projectPath,' #.',projectName
 :Else
     →T.GoToTidyUp 1
 :EndTrap

 projectName2←(⊃⎕SI),'_2'
 projectPath2←F.EnforceSlash ∆PATH,'/',projectName2
 #.⎕SHADOW projectName2
 ref2←⍎projectName2 #.⎕NS''
 ref2.⎕FX'r←Hello' 'r←''Universe'''

 :Trap (~stopFlag)/0
     {}∆RunUcmd'Createproject ',projectPath2,' #.',projectName2
 :Else
     →T.GoToTidyUp 1
 :EndTrap

 #.⎕EX¨projectName projectName2
 #.⎕SHADOW'Temp'
 (bin configFilename)←∆ReadConfigFile projectPath
 configParms←''
 configParms,←⊂'{'
 configParms,←⊂'  "Open": "',projectPath2,' #.Temp -track=off",'
 configParms,←⊂'  "ProjectSpace": "#.',projectName,'"'
 configParms,←⊂'}'
 configParms←,¨configParms
 A.WriteUtf8File configFilename configParms

 :Trap (~stopFlag)/0
     list←∆RunUcmd'OpenProject ',projectPath
 :Else
     →T.GoToTidyUp 1
 :EndTrap

 →T.GoToTidyUp 9=#.⎕NC projectName2
 →T.GoToTidyUp 0=#.⎕NC'Temp'
 →T.GoToTidyUp 3≠#.Temp.⎕NC'Hello'
 →T.GoToTidyUp'Universe'≢#.Temp.Hello
 →T.GoToTidyUp 7≠≢messages←∆GetLog ⍬
 →T.GoToTidyUp 1≠(⊃⍴∆RunUcmd'Projects')-noOfProjects

 R←T.∆OK

∆TidyUp:
 :Trap 0 ⋄ F.RmDir projectPath ⋄ :EndTrap
 :Trap 0 ⋄ F.RmDir projectPath2 ⋄ :EndTrap
 ∆ClearLog
 ∆RunUcmd'Close #.',projectName
 ∆RunUcmd'Close #.',projectName2
⍝Done
