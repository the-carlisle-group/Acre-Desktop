 R←Test_API_024(stopFlag batchFlag);list;projectName;projectPath;ref;result;tno;⎕TRAP
⍝ Exercise the API function `SetChanged`
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←∆Failed

 projectName←⊃⎕SI
 projectPath←∆PATH,'/',projectName
 #.⎕SHADOW projectName
 ref←⍎projectName #.⎕NS''

 result←∆RunUcmd'Createproject ',projectPath,' #.',projectName,' -keephistory=on'
 →GoToTidyUp 0≠≢##.FilesAndDirs.ListFiles projectPath,'\.acre\changes\'

 ref.⎕FX'r←Hello' 'r←''World'''
 result←⎕SE.acre.SetChanged'#.',projectName,'.Hello'
 →GoToTidyUp 0=≢result
 ref.⎕FX'r←Hello' 'r←''Galaxy'''
 result←⎕SE.acre.SetChanged'#.',projectName,'.Hello'
 →GoToTidyUp 0=≢result

 →GoToTidyUp 1≠≢list←##.FilesAndDirs.ListFiles projectPath,'\.acre\changes\'
 tno←(⊃list)⎕FTIE 0
 →GoToTidyUp 3≠-/⌽2↑⎕FSIZE tno

 ref.⎕EX'Hello'
 ref.⎕FX'r←Hello2' 'r←''Universe'''
 ref.⎕FX'r←THE_Answer' 'r←42'
 result←⎕SE.acre.SetChanged'#.',projectName,' -recursive'
 →GoToTidyUp 2≠≢list←##.FilesAndDirs.ListFiles projectPath,'\.acre\changes\'  ⍝ One deleted, two added

 R←∆OK

∆TidyUp:
 :Trap 0 ⋄ ⎕FUNTIE tno ⋄ :EndTrap
 F.RmDir projectPath
 ∆RunUcmd'Close #.',projectName
⍝Done
