 R←Test_Misc_005(stopFlag batchFlag);⎕TRAP;projectPath;ref;projectName;result;list
⍝ Exercise the "PurgeHistory" command.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←T.∆Failed

 projectName←⊃⎕SI
 projectPath←∆PATH,'/',projectName
 #.⎕SHADOW projectName
 ref←⍎projectName #.⎕NS''
 ref.⎕FX'r←Hello' 'r←''World'''

 result←∆RunUcmd'Createproject ',projectPath,' #.',projectName,' -keephistory=on'

 ref.⎕FX'r←Hello' 'r←''World2'''
 result←∆RunUcmd'SetChanged #.',projectName,'.Hello'
 →GoToTidyUp 0∊⍴result
 →GoToTidyUp 1≠⍴F.ListFiles projectPath,'\.acre\changes\'
 →GoToTidyUp 1≠∆RunUcmd'PurgeHistory #.',projectName
 →GoToTidyUp 0≠⍴F.ListFiles projectPath,'\.acre\changes\'

 R←T.∆OK

∆TidyUp:
 F.RmDir projectPath
 ∆ClearLog
 ∆RunUcmd'Close #.',projectName
⍝Done
