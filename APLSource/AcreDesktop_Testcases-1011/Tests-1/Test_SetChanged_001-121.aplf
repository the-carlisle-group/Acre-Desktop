 R←Test_SetChanged_001(stopFlag batchFlag);list;projectName;projectPath;ref;result;tno;⎕TRAP
⍝ Exercise the "SetChanged" command on a project that was just created. See also Test_Misc_010
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←T.∆Failed

 projectName←⊃⎕SI
 projectPath←∆PATH,'/',projectName
 #.⎕SHADOW projectName
 ref←⍎projectName #.⎕NS''

 result←∆RunUcmd'Createproject ',projectPath,' #.',projectName,' -keephistory=on'
 →T.GoToTidyUp 0≠≢list←F.ListFiles projectPath,'\.acre\changes\'

 ref.⎕FX'r←Hello' 'r←''World'''
 result←∆RunUcmd'SetChanged #.',projectName,'.Hello'
 →T.GoToTidyUp 0=≢result
 ref.⎕FX'r←Hello' 'r←''Universe'''
 result←∆RunUcmd'SetChanged #.',projectName,'.Hello'
 →T.GoToTidyUp 0=≢result

 →T.GoToTidyUp 1≠≢list←F.ListFiles projectPath,'\.acre\changes\'
 tno←(⊃list)⎕FTIE 0
 →T.GoToTidyUp 3≠-/⌽2↑⎕FSIZE tno   ⍝ Orig, change, pointer

 R←T.∆OK

∆TidyUp:
 :Trap 0 ⋄ ⎕FUNTIE tno ⋄ :EndTrap
 F.RmDir projectPath
 ∆RunUcmd'Close #.',projectName
⍝Done
