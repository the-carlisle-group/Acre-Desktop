 R←Test_CreateProject_007(stopFlag batchFlag);⎕TRAP;projectPath;ref;projectName;result;list;noOf;expected
⍝ Create a project with a public method in a class.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←T._Failed

 projectName←⊃⎕SI
 projectPath←∆PATH,'/',projectName
 #.⎕SHADOW projectName
 ref←⍎projectName #.⎕NS''
 ref.⎕FIX':Class Foo' '∇r←Hello' ':Access Public Shared' 'r←''World''' '∇' ':EndClass'
 noOf←⊃⍴∆RunUcmd'Projects'
 ∆ClearLog

 result←∆RunUcmd'Createproject ',projectPath,' #.',projectName,' -casecode=on'
 →T.GoToTidyUp 0∊⍴result
 →T.GoToTidyUp 0=F.IsDir projectPath,'/APLSource'
 →T.GoToTidyUp 0=F.IsFile projectPath,'/acre.config'
 list←'/'##.APLTreeUtils.Last¨F.EnforceSlash¨⊃F.Dir projectPath,'/APLSource/*'
 expected←'Foo-1.aplc' 'quadVars-10.apln'  ⍝ The latter is added automatically!
 →T.GoToTidyUp~list ∆LazyMatch expected

 R←T._OK

∆TidyUp:
 F.RmDir projectPath
 ∆ClearLog
 ∆RunUcmd'CloseProject #.',projectName
⍝Done
