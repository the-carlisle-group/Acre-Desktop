 R←Test_SetChanged_003(stopFlag batchFlag);⎕TRAP;projectName;projectPath;result;ref
⍝ Exercise the "SetChanged" command on a project that was opened with -track=off: should have no effect.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←T.T._Failed

 projectName←⊃⎕SI
 projectPath←∆PATH,'/',projectName
 #.⎕SHADOW projectName
 ref←⍎projectName #.⎕NS''
 ref.⎕FX'r←Hello' 'r←''World'''

 :Trap (~stopFlag)/0
     result←∆RunUcmd'Createproject ',projectPath,' #.',projectName ⍝ QuadVars is added automatically
 :Else
     →T.GoToTidyUp 1
 :EndTrap
 →T.GoToTidyUp result≢'#.',projectName

 result←∆RunUcmd'SetChanged #.',projectName,'.Hello'
 →T.GoToTidyUp result≢,⊂'#.',projectName,'.Hello'
 ref.⎕FX'r←Hello' 'r←''Universe'''
 result←∆RunUcmd'SetChanged #.',projectName,'.Hello'
 →T.GoToTidyUp result≢,⊂'#.',projectName,'.Hello'
 #.⎕EX projectName
 result←∆RunUcmd'OpenProject ',projectPath,' -track=off'
 →T.GoToTidyUp result≢'#.',projectName
 ref←#.⍎projectName
 ref.⎕FX'r←Hello' 'r←''Rubbish'''
 result←∆RunUcmd'SetChanged #.',projectName,'.Hello'
 →T.GoToTidyUp 0≠≢result
 #.⎕EX projectName
 ∆RunUcmd'OpenProject ',projectPath,' -track=off'
 ref←#.⍎projectName
 →T.GoToTidyUp'Universe'≢ref.Hello

 R←T.T._OK

∆TidyUp:
 F.RmDir projectPath
 ∆RunUcmd'Close #.',projectName
⍝Done
