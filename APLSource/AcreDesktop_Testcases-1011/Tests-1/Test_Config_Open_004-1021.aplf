 R←Test_Config_Open_004(stopFlag batchFlag);⎕TRAP;noOfProjects;projectName;projectPath;ref;projectName2;projectPath2;ref2;projectName3;projectPath3;ref3;bin;configFilename;configParms;list;messages;json
⍝ Create a project and check whether `acre.config[Dependencies]` is honored (two projects).
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←T.T._Failed

 :If 16>⊃(//)⎕VFI{⍵/⍨2>+\'.'=⍵}2⊃# ⎕WG'APLVersion'
     →0,R←∆InvalidAplVersion
 :EndIf

 noOfProjects←⊃⍴∆RunUcmd'Projects'
 projectName←⊃⎕SI
 projectPath←F.EnforceSlash ∆PATH,'/',projectName
 #.⎕SHADOW projectName
 ref←⍎projectName #.⎕NS''
 ref.⎕FX'r←Hello' 'r←''World'''
 ∆ClearLog

 :Trap (~stopFlag)/0
     {}∆RunUcmd'Createproject "',projectPath,'" #.',projectName
 :Else
     →T.GoToTidyUp 1
 :EndTrap
 →T.GoToTidyUp 1≠≢messages←∆GetLog ⍬

 projectName2←(⊃⎕SI),'_2'
 projectPath2←F.EnforceSlash ∆PATH,'/',projectName2
 #.⎕SHADOW projectName2
 ref2←⍎projectName2 #.⎕NS''
 ref2.⎕FX'r←Hello' 'r←''Universe'''

 :Trap (~stopFlag)/0
     {}∆RunUcmd'Createproject "',projectPath2,'" #.',projectName2
 :Else
     →T.GoToTidyUp 1
 :EndTrap
 →T.GoToTidyUp 3≠≢messages←∆GetLog ⍬

 projectName3←(⊃⎕SI),'_3'
 projectPath3←F.EnforceSlash ∆PATH,'/',projectName3
 #.⎕SHADOW projectName3
 ref3←⍎projectName3 #.⎕NS''
 ref3.⎕FX'r←Hello' 'r←''Galaxy'''

 :Trap (~stopFlag)/0
     {}∆RunUcmd'Createproject "',projectPath3,'" #.',projectName3
 :Else
     →T.GoToTidyUp 1
 :EndTrap
 →T.GoToTidyUp 5≠≢messages←∆GetLog ⍬

 #.⎕EX¨projectName projectName2 projectName3
 #.⎕SHADOW¨'Temp' 'Temp2'
 (bin configFilename)←∆ReadConfigFile projectPath
 configParms←⎕NS''
 configParms.Dependencies←⊂'"',projectPath2,'" #.Temp'
 configParms.Dependencies,←⊂,'"',projectPath3,'" #.Temp2'
 configParms.ProjectSpace←'#.',projectName
 json←⎕JSON configParms
 A.WriteUtf8File configFilename json

 :Trap (~stopFlag)/0
     list←∆RunUcmd'OpenProject ',projectPath
 :Else
     →T.GoToTidyUp 1
 :EndTrap

 →T.GoToTidyUp 9=#.⎕NC projectName2
 →T.GoToTidyUp 9=#.⎕NC projectName3
 →T.GoToTidyUp 0=#.⎕NC'Temp'
 →T.GoToTidyUp 0=#.⎕NC'Temp2'
 →T.GoToTidyUp 3≠#.Temp.⎕NC'Hello'
 →T.GoToTidyUp 3≠#.Temp2.⎕NC'Hello'
 →T.GoToTidyUp'Universe'≢#.Temp.Hello
 →T.GoToTidyUp'Galaxy'≢#.Temp2.Hello
 ⎕DL 0.2
 →T.GoToTidyUp 13≠≢messages←∆GetLog ⍬
 →T.GoToTidyUp 3≠(⊃⍴∆RunUcmd'Projects')-noOfProjects

 R←T.T._OK

∆TidyUp:
 :Trap 0 ⋄ F.RmDir projectPath ⋄ :EndTrap
 :Trap 0 ⋄ F.RmDir projectPath2 ⋄ :EndTrap
 :Trap 0 ⋄ F.RmDir projectPath3 ⋄ :EndTrap
 ∆ClearLog
 :Trap 0 ⋄ ∆RunUcmd'Close #.',projectName ⋄ :EndTrap
 :Trap 0 ⋄ ∆RunUcmd'Close #.',projectName2 ⋄ :EndTrap
 :Trap 0 ⋄ ∆RunUcmd'Close #.',projectName3 ⋄ :EndTrap
⍝Done
