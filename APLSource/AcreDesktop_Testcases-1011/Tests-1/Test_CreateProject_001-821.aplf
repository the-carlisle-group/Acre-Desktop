 R←Test_CreateProject_001(stopFlag batchFlag);⎕TRAP;path;rc;en;more;projectPath;ref;projectName;result;list
⍝ Create a project with a specified namespace, check that it is open as well as the number of files in `APLSource/`
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←∆Failed

 projectName←⊃⎕SI
 projectPath←∆PATH,'/',projectName
 #.⎕SHADOW projectName
 ref←⍎projectName #.⎕NS''
 ref.⎕FX'r←Hello' 'r←''World'''
 ∆ClearLog

 :Trap (~stopFlag)/0
     result←∆RunUcmd'Createproject ',projectPath,' #.',projectName,' -casecode=on'
 :Else
     →GoToTidyUp 1
 :EndTrap

 →GoToTidyUp 0=F.IsDir projectPath,'/APLSource'
⍝ →GoToTidyUp 0=F.IsDir projectPath,'/.acre' ⍝TODO⍝ /.acre/ now only exists if -keephistory is on
 →GoToTidyUp 0=F.IsFile projectPath,'/acre.config'
 →GoToTidyUp(F.EnforceSlash projectPath,'/APLSource/Hello-1.aplf')≢F.EnforceSlash⊃⊃F.Dir projectPath,'/APLSource/*'

 R←∆OK

∆TidyUp:
 F.RmDir projectPath
 ∆ClearLog
 ∆RunUcmd'Close #.',projectName
⍝Done
