 R←Test_Config_UserExit_002(stopFlag batchFlag);⎕TRAP;configFilename;bin;buff;list;messages;projectPath
⍝ Create a project and check whether `acre.config[AfterSave]` is honored.
⍝ This is the user exit that allows the execution of any source code management command like Git's "Commit".
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 :If batchFlag
     →0,R←T._NoBatchTest
 :EndIf

 R←T._Failed

 ∆ClearLog
 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 Assert' '=1↑0⍴⊃#.TEMP.⎕FX↑'r←x UserExit arg' 'r←⍬' '∆FLAG←1' '∆ConfigNS←x'
 Assert' '=1↑0⍴⊃#.TEMP.⎕FX↑'r←Hello' '⍝ PLEASE: change me!'
 #.TEMP.∆FLAG←0
 projectPath←F.GetTempPath,⊃⎕SI
 F.RmDir projectPath

 :Trap (~stopFlag)/0
     {}'-variables=on'∆RunUcmd'Createproject "',projectPath,'" #.TEMP'
 :Else
     →T.GoToTidyUp 1
 :EndTrap
 →T.GoToTidyUp 1≠≢messages←∆GetLog ⍬

 ∆RunUcmd'Close #.TEMP'
 #.⎕EX'TEMP'

 (bin configFilename)←∆ReadConfigFile projectPath
 buff←''
 buff,←⊂'{'
 buff,←⊂'  "Open": "",'
 buff,←⊂'  "ProjectSpace": "#.TEMP",'
 buff,←⊂'  "AfterSave": "#.TEMP.UserExit"'
 buff,←⊂'}'
 buff←,¨buff
 A.WriteUtf8File configFilename buff

 :Trap (~stopFlag)/0
     list←∆RunUcmd'OpenProject ',projectPath
 :Else
     →T.GoToTidyUp 1
 :EndTrap

 #.TEMP.⎕ED'Hello'
 →T.PassesIf #.TEMP.∆FLAG                 ⍝ Modified by the user exit?
 →T.PassesIf 9=⎕NC'#.TEMP.∆ConfigNS'      ⍝ Should be a namespace clone representing acre's config file
 →T.PassesIf 5<≢#.TEMP.∆ConfigNS.⎕NL⍳16   ⍝ At least 5 config options

 R←T._OK

∆TidyUp:
 :Trap 0 ⋄ F.RmDir projectPath ⋄ :EndTrap
 ∆ClearLog
 ∆RunUcmd'Close #.TEMP'
⍝Done
