 R←Test_Config_UserExit_002(stopFlag batchFlag);⎕TRAP;configFilename;bin;buff;list;messages;projectPath
⍝ Create a project and check whether `acre.config[SCMexit]` is honored.
⍝ This is the user exit that allows the execution of any source code management command like Git's "Commit".
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 :If batchFlag
     →0,R←∆NoBatchTest
 :EndIf

 R←∆Failed

 ∆ClearLog
 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 Assert' '=1↑0⍴⊃#.TEMP.⎕FX↑'r←{a}UserExit arg' '⎕←''***''' '⎕←r←arg' '⎕←''***''' '∆FLAG←1'
 Assert' '=1↑0⍴⊃#.TEMP.⎕FX↑'r←Hello' '⍝ PLEASE: change me!'
 #.TEMP.∆FLAG←0
 projectPath←F.GetTempPath,⊃⎕SI
 F.RmDir projectPath

 :Trap (~stopFlag)/0
     {}'-variables=on'∆RunUcmd'Createproject "',projectPath,'" #.TEMP'
 :Else
     →GoToTidyUp 1
 :EndTrap
 →GoToTidyUp 1≠≢messages←∆GetLog ⍬

 ∆RunUcmd'Close #.TEMP'
 #.⎕EX'TEMP'

 (bin configFilename)←∆ReadConfigFile projectPath
 buff←''
 buff,←⊂':Namespace'
 buff,←⊂'   Open←'''''
 buff,←⊂'   ProjectSpace←''#.TEMP'''
 buff,←⊂'   SCMexit←''#.TEMP.UserExit'''
 buff,←⊂':EndNamespace'
 A.WriteUtf8File configFilename buff

 :Trap (~stopFlag)/0
     list←∆RunUcmd'OpenProject ',projectPath
 :Else
     →GoToTidyUp 1
 :EndTrap

 #.TEMP.⎕ED'Hello'
 →PassesIf #.TEMP.∆FLAG  ⍝ Modified by the user exit?

 R←∆OK

∆TidyUp:
 :Trap 0 ⋄ F.RmDir projectPath ⋄ :EndTrap
 ∆ClearLog
 ∆RunUcmd'Close #.TEMP'
⍝Done
