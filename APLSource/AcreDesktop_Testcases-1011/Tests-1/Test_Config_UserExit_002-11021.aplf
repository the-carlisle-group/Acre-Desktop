 R←Test_Config_UserExit_002(stopFlag batchFlag);⎕TRAP;noOfProjects;projectName;projectPath;ref;configFilename;bin;buff;list;messages
⍝ Create a project and check whether `acre.config[SCMexit]` is honored.
⍝ This is the user exit that allows the execution of any source code management command like Git's "Commit".
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 :If batchFlag
     →0,R←∆NoBatchTest
 :EndIf

 R←∆Failed

 noOfProjects←⊃⍴∆RunUcmd'Projects'
 projectName←⊃⎕SI
 projectPath←F.EnforceSlash ∆PATH,'/',projectName
 #.⎕SHADOW projectName
 ref←⍎projectName #.⎕NS''
 ref.⎕FX'r←Hello' '⍝ PLEASE: make a change to this funtion!' 'r←''World'''
 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 #.TEMP.⎕FX↑'UserExit arg' '⎕←''***''' '⎕←arg' '⎕←''***''' '∆FLAG←1'
 #.TEMP.∆FLAG←0
 ∆ClearLog

 :Trap (~stopFlag)/0
     {}∆RunUcmd'Createproject "',projectPath,'" #.',projectName
 :Else
     →GoToTidyUp 1
 :EndTrap
 →GoToTidyUp 2≠≢messages←∆GetLog ⍬

 #.⎕EX projectName
 (bin configFilename)←∆ReadConfigFile projectPath
 buff←''
 buff,←⊂':Namespace'
 buff,←⊂'   Open←'''''
 buff,←⊂'   ProjectSpace←''#.',projectName,''''
 buff,←⊂'   SCMexit←''#.TEMP.UserExit'''
 buff,←⊂':EndNamespace'
 A.WriteUtf8File configFilename buff

 :Trap (~stopFlag)/0
     list←∆RunUcmd'OpenProject ',projectPath
 :Else
     →GoToTidyUp 1
 :EndTrap

 ref.⎕ED'Hello'
 →PassesIf #.TEMP.∆FLAG  ⍝ Modified by the user exit?

 R←∆OK

∆TidyUp:
 :Trap 0 ⋄ F.RmDir projectPath ⋄ :EndTrap
 ∆ClearLog
⍝Done
