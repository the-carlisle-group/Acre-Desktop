 R←Test_Run_002(stopFlag batchFlag);⎕TRAP;projectName;projectPath;ref;changeFilename;changePath;aplsourcePath;changeFilenames
⍝ Exercise the `run` function on "changefile".
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←∆Failed

 projectName←⊃⎕SI
 projectPath←F.EnforceSlash ∆PATH,'\',projectName
 #.⎕SHADOW projectName
 ref←⍎projectName #.⎕NS''
 {}⎕SE.UCMD'CreateProject  "',projectPath,'" #.',projectName,' -messages=off'

 ref.⎕FX'r←Hello' 'r←''World'''
 {}⎕SE.UCMD'acre.SetChanged #.',projectName,'.Hello'
 ref.⎕FX'r←Hello2' 'r←''Whole universe'''
 {}⎕SE.UCMD'acre.SetChanged #.',projectName,'.Hello2'
 changeFilename←'changefile'⎕SE.acre.run'#.',projectName,'.Hello'
 changePath←F.EnforceSlash 1⊃⎕NPARTS changeFilename
 →GoToTidyUp projectPath≢(⍴projectPath)↑changePath
 →GoToTidyUp 1≠⍴⊃F.Dir changeFilename
 aplsourcePath←('Expand'F.NormalizePath changePath,'..\..'),'\APLSource'
 →GoToTidyUp 3≠⍴⊃F.Dir aplsourcePath,'\*'       ⍝ Quad-Vars script is added automatically
 changeFilenames←'changefile'∘⎕SE.acre.run¨('#.',projectName,'.Hello')('#.',projectName,'.Hello2')
 →GoToTidyUp 2≠⍴changeFilenames
 →GoToTidyUp 1 1≢≡¨changeFilenames
 →GoToTidyUp~∧/projectPath∘{⍺≡(⍴⍺)↑⍵}¨changeFilenames

 changeFilename←'changefile'⎕SE.acre.run'#.',projectName,'.Hello_DoesNotExist'
 →GoToTidyUp 1≠≡changeFilename
 →GoToTidyUp~changePath{⍺≡(⍴⍺)↑⍵}changeFilename
 →GoToTidyUp'.*.change'{⍺≢(-⍴⍺)↑⍵}(⍴changePath)↓changeFilename

 →GoToTidyUp~0∊⍴'changefile'⎕SE.acre.run'#.DoesNotExistAtAll'

 R←∆OK

∆TidyUp:
 F.RmDir projectPath
