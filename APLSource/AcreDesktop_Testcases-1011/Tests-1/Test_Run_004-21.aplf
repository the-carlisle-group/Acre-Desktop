 R←Test_Run_004(stopFlag batchFlag);⎕TRAP;projectName;projectPath;ref;changePath;aplsourcePath;result;deleteFilename;deleteFilenames
⍝ Exercise the `run` function on "deletefile".
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←∆Failed

 projectName←⊃⎕SI
 projectPath←∆PATH,'\',projectName
 #.⎕SHADOW projectName
 ref←⍎projectName #.⎕NS''
 {}∆RunUcmd'CreateProject  "',projectPath,'" #.',projectName

 ref.⎕FX'r←Hello' 'r←''World'''
 {}∆RunUcmd'SetChanged #.',projectName,'.Hello'
 ref.⎕FX'r←Hello2' 'r←''Whole universe'''
 {}∆RunUcmd'SetChanged #.',projectName,'.Hello2'
 deleteFilename←⊃'deletefile'⎕SE.acre.run'#.',projectName,'.Hello'
 result←∆RunUcmd'Erase #.',projectName,'.Hello'
 →GoToTidyUp result≢,⊂'#.Test_Run_004.Hello'
 deleteFilename←'deletefile'⎕SE.acre.run'#.',projectName,'.Hello2'
 result←∆RunUcmd'Erase #.',projectName,'.Hello2'
 →GoToTidyUp result≢,⊂'#.Test_Run_004.Hello2'
 changePath←F.EnforceBackslash 1⊃⎕NPARTS deleteFilename
 →GoToTidyUp projectPath≢(⍴projectPath)↑changePath
 →GoToTidyUp 2≠⍴⊃F.Dir changePath
 →GoToTidyUp~F.IsFile deleteFilename
 deleteFilenames←##.FilesAndDirs.NormalizePath'deletefile'∘⎕SE.acre.run¨('#.',projectName,'.Hello')('#.',projectName,'.Hello2')
 →GoToTidyUp 1 1≢≡¨deleteFilenames
 →GoToTidyUp 2≠⍴deleteFilenames
 →GoToTidyUp~∧/projectPath∘{⍺≡(⍴⍺)↑⍵}¨deleteFilenames

 →GoToTidyUp 0≠≢'Deletefile'⎕SE.acre.run'#.DoesNotExistAtAll'

 R←∆OK

∆TidyUp:
 F.RmDir projectPath
 ∆RunUcmd'Close #.',projectName
⍝Done
