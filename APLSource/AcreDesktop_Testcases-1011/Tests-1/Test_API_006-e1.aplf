 R←Test_API_006(stopFlag batchFlag);⎕TRAP
⍝ Exercise the API function `Nameclass`.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←T.∆Failed

 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''

 →T.PassesIf''≡1⊃⎕SE.acre.Nameclass'#.TEMP'

 →T.PassesIf'.*'≡1⊃⎕SE.acre.Nameclass'DoesNotExistForSure'

 #.TEMP.Foo←'abc'
 →T.PassesIf'.charstring'≡1⊃⎕SE.acre.Nameclass'#.TEMP.Foo'

 #.TEMP.Foo←2 2⍴'abc'
 →T.PassesIf'.charmat'≡1⊃⎕SE.acre.Nameclass'#.TEMP.Foo'

 #.TEMP.Foo←'abc' 'def'
 →T.PassesIf'.charlist'≡1⊃⎕SE.acre.Nameclass'#.TEMP.Foo'

 #.TEMP.Foo←1 2 3 'abc' 'def'
 →T.PassesIf'.array'≡1⊃⎕SE.acre.Nameclass'#.TEMP.Foo'

 #.TEMP.Foo2←'abc'
 →T.PassesIf'.array' '.charstring'≡⎕SE.acre.Nameclass'#.TEMP.Foo' '#.TEMP.Foo2'

 #.TEMP.⎕EX'Foo' ⋄ #.TEMP.Foo←{+/⍵}
 →T.PassesIf'.aplf'≡1⊃⎕SE.acre.Nameclass'#.TEMP.Foo'

 #.TEMP.⎕EX'Foo' ⋄ #.TEMP.Foo←{⍺⍺+/⍵}
 →T.PassesIf'.aplo'≡1⊃⎕SE.acre.Nameclass'#.TEMP.Foo'

 #.TEMP.⎕EX'Foo' ⋄ #.TEMP.⎕FIX':Class Foo' ':EndClass'
 →T.PassesIf'.aplc'≡1⊃⎕SE.acre.Nameclass'#.TEMP.Foo'

 #.TEMP.⎕EX'Foo' ⋄ #.TEMP.⎕FIX':Namespace Foo' ':EndNamespace'
 →T.PassesIf'.apln'≡1⊃⎕SE.acre.Nameclass'#.TEMP.Foo'

 #.TEMP.⎕EX'Foo' ⋄ #.TEMP.⎕FIX':Interface Foo' ':EndInterface'
 →T.PassesIf'.apli'≡1⊃⎕SE.acre.Nameclass'#.TEMP.Foo'

 R←T.∆OK
⍝Done
