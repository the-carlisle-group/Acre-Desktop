 R←Test_API_006(stopFlag batchFlag);⎕TRAP
⍝ Exercise the API function `nameclass`.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←∆Failed

 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''

 →PassesIf''≡0⊃⎕SE.acreAPI.nameclass'#.TEMP'

 →PassesIf'.*'≡0⊃⎕SE.acreAPI.nameclass'DoesNotExistForSure'

 #.TEMP.Foo←'abc'
 →PassesIf'.charstring'≡0⊃⎕SE.acreAPI.nameclass'#.TEMP.Foo'

 #.TEMP.Foo←2 2⍴'abc'
 →PassesIf'.charmat'≡0⊃⎕SE.acreAPI.nameclass'#.TEMP.Foo'

 #.TEMP.Foo←'abc' 'def'
 →PassesIf'.charlist'≡0⊃⎕SE.acreAPI.nameclass'#.TEMP.Foo'

 #.TEMP.Foo←1 2 3 'abc' 'def'
 →PassesIf'.array'≡0⊃⎕SE.acreAPI.nameclass'#.TEMP.Foo'

 #.TEMP.Foo2←'abc'
 →PassesIf'.array' '.charstring'≡⎕SE.acreAPI.nameclass'#.TEMP.Foo' '#.TEMP.Foo2'

 #.TEMP.⎕EX'Foo' ⋄ #.TEMP.Foo←{+/⍵}
 →PassesIf'.aplf'≡0⊃⎕SE.acreAPI.nameclass'#.TEMP.Foo'

 #.TEMP.⎕EX'Foo' ⋄ #.TEMP.Foo←{⍺⍺+/⍵}
 →PassesIf'.aplo'≡0⊃⎕SE.acreAPI.nameclass'#.TEMP.Foo'

 #.TEMP.⎕EX'Foo' ⋄ #.TEMP.⎕FIX':Class Foo' ':EndClass'
 →PassesIf'.aplc'≡0⊃⎕SE.acreAPI.nameclass'#.TEMP.Foo'

 #.TEMP.⎕EX'Foo' ⋄ #.TEMP.⎕FIX':Namespace Foo' ':EndNamespace'
 →PassesIf'.apln'≡0⊃⎕SE.acreAPI.nameclass'#.TEMP.Foo'

 #.TEMP.⎕EX'Foo' ⋄ #.TEMP.⎕FIX':Interface Foo' ':EndInterface'
 →PassesIf'.apli'≡0⊃⎕SE.acreAPI.nameclass'#.TEMP.Foo'

 R←∆OK
⍝Done
